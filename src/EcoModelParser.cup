/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Copyright (C) 2001       Gerwin Klein <lsf@jflex.de>                    *
 * Copyright (C) 2001       Bernhard Rumpe <rumpe@in.tum.de>               *
 * All rights reserved.                                                    *
 *                                                                         *
 * License: BSD                                                            *
 *                                                                         *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */


// definition of tokens, if applicable with token type 
terminal CON, LET, SIGMA, FORALL;
terminal LEFTAR, RIGHTAR, LEFTB, RIGHTB, COMMA, SEMICOLON;
terminal LPAR, RPAR, LEFTCB, RIGHTCB;
terminal MINUS, PLUS, TIMES, DIV, UMINUS;
terminal EQ, LE, LEQ, GE, GEQ, AND, OR;
terminal String VALUE, INDEX, ID, STRINGLITERAL;
terminal TRUE, FALSE;
terminal IF, THEN, ELSE, ENDIF;
terminal WHILE, DO, ENDWHILE;
terminal CSV;

non terminal jp.co.mra.ecodsl.command.Tprogram  program;
non terminal jp.co.mra.ecodsl.exp.Texplist  explist;
non terminal jp.co.mra.ecodsl.command.Topelist opelist;
non terminal jp.co.mra.ecodsl.command.Tope      ope;
non terminal jp.co.mra.ecodsl.exp.Texp      exp;
non terminal jp.co.mra.ecodsl.exp.Tboolexp  boolexp;
non terminal jp.co.mra.ecodsl.exp.Tident    ident;
non terminal jp.co.mra.ecodsl.exp.Tvalue   value;
non terminal jp.co.mra.ecodsl.exp.Tindex   index;
non terminal jp.co.mra.ecodsl.command.Tlet   let;
non terminal jp.co.mra.ecodsl.exp.Tstring   string;
non terminal jp.co.mra.ecodsl.command.Tifthenelse   if;
non terminal jp.co.mra.ecodsl.command.Tprewhile   prewhile;
non terminal jp.co.mra.ecodsl.command.Tpostwhile  postwhile;
non terminal jp.co.mra.ecodsl.exp.Tcsv  csv;
//non terminal Tsigma   sigma;


// precedences, left associativity
precedence left EQ, LE, LEQ, GE, GEQ, OR;
precedence left MINUS, PLUS, OR;
precedence left TIMES, DIV, AND;
//precedence right COMMA, SEMICOLON;

// here the rules start 

program       ::= opelist:ol
                  {: RESULT = new jp.co.mra.ecodsl.command.Tprogram(ol); :}
                ;

explist       ::= exp:e
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texplist(e); :}
                | exp:e COMMA explist:l
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texplist(e, l); :}
                ;

opelist      ::= ope:o
                  {: RESULT = new jp.co.mra.ecodsl.command.Topelist(o);:}
                | opelist:l ope:o
                  {: RESULT = new jp.co.mra.ecodsl.command.Topelist(l,o); :}
                ;

ope          ::= let:l SEMICOLON
                  {: RESULT = new jp.co.mra.ecodsl.command.Tope(l); :}
                | if:i SEMICOLON
                  {: RESULT = new jp.co.mra.ecodsl.command.Tope(i); :}
                | prewhile:w SEMICOLON
                  {: RESULT = new jp.co.mra.ecodsl.command.Tope(w); :}
                | postwhile:w SEMICOLON
                  {: RESULT = new jp.co.mra.ecodsl.command.Tope(w); :}
                 ;

exp           ::= value:n
                  {: RESULT = n; :}
                | index:i
                  {: RESULT = i; :}
                | ident:i
                  {: RESULT = i; :}
                | string:s
                  {: RESULT = s; :}
                | LPAR exp:e RPAR
                  {: RESULT = e; :}
                | LEFTB explist:e RIGHTB
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texplist(e); :}
                | LEFTB RIGHTB
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texplist(); :}
                | LEFTAR explist:e RIGHTAR
                  {: RESULT = new jp.co.mra.ecodsl.exp.TexpArray(e); :}
                | exp:l PLUS exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texpinfix(l,'+',r); :}
                | exp:l TIMES exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texpinfix(l,'*',r); :}
                | exp:l DIV exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texpinfix(l,'/',r); :}
                | exp:l MINUS exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Texpinfix(l,'-',r); :}
                | SIGMA LPAR exp:e RPAR
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tsigma(e); :}
                ;

boolexp       ::= 
                  TRUE
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(true); :}
                | FALSE
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(false); :}
                | exp:l EQ exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(l,'=',r); :}
                | exp:l LE exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(l,'<',r); :}
                | exp:l LEQ exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(l,'!',r); :}
                | exp:l GE exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(l,'>',r); :}
                | exp:l GEQ exp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(l,'$',r); :}
                | boolexp:l AND boolexp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(l,'&',r); :}
                | boolexp:l OR boolexp:r
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tboolexp(l,'|',r); :}
                | LPAR boolexp:e RPAR
                  {: RESULT = e; :}
                ;

ident         ::= ID:n
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tident(n); :}
                ;

value        ::= VALUE:z
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tvalue(z); :}
                ;

index        ::= INDEX:z
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tindex(z); :}
                ;

string       ::= STRINGLITERAL:s
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tstring(s); :}
                ;

let           ::= 
                  ident:i LET csv:c
                  {: RESULT = new jp.co.mra.ecodsl.command.Tlet(i, c); :}
                  |
                  csv:c LET exp:e
                  {: RESULT = new jp.co.mra.ecodsl.command.Tlet(c, e); :}
                  |
                  CON LET exp:e
                  {: RESULT = new jp.co.mra.ecodsl.command.Tlet(e); :}
                  |
                  ident:i LET exp:e
                  {: RESULT = new jp.co.mra.ecodsl.command.Tlet(i,e); :}
                 ;
if            ::=
                  IF boolexp:b THEN opelist:t ELSE opelist:e ENDIF
                  {: RESULT = new jp.co.mra.ecodsl.command.Tifthenelse(b,t,e); :}
                | IF boolexp:b THEN opelist:t 
                  {: RESULT = new jp.co.mra.ecodsl.command.Tifthenelse(b,t); :}
                ;

prewhile       ::=
                  WHILE LPAR boolexp:b RPAR LEFTCB opelist:l RIGHTCB
                  {: RESULT = new jp.co.mra.ecodsl.command.Tprewhile(b, l);  :}
                ;

postwhile      ::=
                  DO LEFTCB opelist:l RIGHTCB WHILE LPAR boolexp:b RPAR 
                  {: RESULT = new jp.co.mra.ecodsl.command.Tpostwhile(b, l);  :}
                ;

csv            ::=
                  CSV string:file LEFTB explist:l RIGHTB
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tcsv(file, l);  :}         // ヘッダ行が無い場合はカラム名をリストで渡す（）
                  |
                  CSV string:file
                  {: RESULT = new jp.co.mra.ecodsl.exp.Tcsv(file);  :}         // ヘッダ行がある場合はカラム名を渡さない（最初の行をカラム名と解釈する）
                ;
